# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if ((defined(ark_standalone_build) && ark_standalone_build) ||
    (defined(ark_static_standalone_build) && ark_static_standalone_build)) {
  import("//arkcompiler/runtime_core/static_core/ark_config.gni")
} else {
  import(
      "//build/config/components/runtime_core/static_core/ark_common_config.gni")
}

if (ark_standalone_build) {
  import("$build_root/ark.gni")
} else {
  import("//build/ohos.gni")
}

declare_args() {
  lsp_build_enable = false
  bindings_build_enable = false
}

group("lsp_packages") {
  if (lsp_build_enable) {
    deps = [ ":libes2panda_lsp" ]
  }
  if (bindings_build_enable) {
    deps = [ "../bindings:ts_bindings" ]
  }
}

config("libes2panda_lsp_config") {
  include_dirs = [ "include" ]
  cflags_cc = [ "-Wno-return-type-c-linkage" ]
}

ohos_source_set("libes2panda_lsp_static") {
  sources = [
    "src/api.cpp",
    "src/applicable_refactors.cpp",
    "src/brace_matching.cpp",
    "src/cancellation_token.cpp",
    "src/class_hierarchy.cpp",
    "src/class_hierarchy_info.cpp",
    "src/classifier.cpp",
    "src/code_fix_provider.cpp",
    "src/completions.cpp",
    "src/completions_details.cpp",
    "src/create_type_help_items.cpp",
    "src/find_references.cpp",
    "src/find_rename_locations.cpp",
    "src/find_safe_delete_location.cpp",
    "src/formatting/formatting.cpp",
    "src/formatting/formatting_context.cpp",
    "src/formatting/formatting_settings.cpp",
    "src/formatting/rules.cpp",
    "src/formatting/rules_map.cpp",
    "src/generate_constructor.cpp",
    "src/get_adjusted_location.cpp",
    "src/get_class_property_info.cpp",
    "src/get_definition_and_bound_span.cpp",
    "src/get_name_or_dotted_name_span.cpp",
    "src/get_safe_delete_info.cpp",
    "src/inlay_hints.cpp",
    "src/internal_api.cpp",
    "src/isolated_declaration.cpp",
    "src/line_column_offset.cpp",
    "src/navigate_to.cpp",
    "src/organize_imports.cpp",
    "src/quick_info.cpp",
    "src/refactor_provider.cpp",
    "src/refactors/convert_chain.cpp",
    "src/refactors/convert_export.cpp",
    "src/refactors/convert_function.cpp",
    "src/refactors/convert_import.cpp",
    "src/refactors/convert_template.cpp",
    "src/refactors/refactor_types.cpp",
    "src/references.cpp",
    "src/register_code_fix/add_missing_declare_property.cpp",
    "src/register_code_fix/fix_missing_call_parantheses.cpp",
    "src/register_code_fix/fix_nan_equality.cpp",
    "src/register_code_fix/forgetten_this_property_access.cpp",
    "src/register_code_fix/import_fixes.cpp",
    "src/rename.cpp",
    "src/register_code_fix/ui_plugin_suggest.cpp",
    "src/script_element_kind.cpp",
    "src/services/services.cpp",
    "src/services/text_change/change_tracker.cpp",
    "src/services/utilities.cpp",
    "src/signature_help.cpp",
    "src/signature_help_items.cpp",
    "src/string_completions.cpp",
    "src/suggestion_diagnostics.cpp",
    "src/todo_comments.cpp",
    "src/types.cpp",
  ]
  if ((defined(ark_standalone_build) && ark_standalone_build) ||
      (defined(ark_static_standalone_build) && ark_static_standalone_build)) {
    configs = [ "$ark_root:ark_config" ]
  } else {
    configs = [
      "//build/config/components/runtime_core/static_core:ark_common_config",
    ]
  }
  configs += [
    "$ark_root/assembler:arkassembler_public_config",
    "../:libes2panda_public_config",
    "../:libes2panda_config",
    "$ark_root/libpandabase:arkbase_public_config",
    "$ark_root/libpandafile:arkfile_public_config",
    ":libes2panda_lsp_config",
  ]

  deps = [
    "../:libes2panda_frontend_static",
    "../:libes2panda_public_frontend_static",
  ]

  external_deps = [
    "runtime_core:libarktsbytecodeopt_package",
    sdk_libc_secshared_dep,
  ]
  part_name = "ets_frontend"
  subsystem_name = "arkcompiler"
}

ohos_shared_library("libes2panda_lsp") {
  deps = [ ":libes2panda_lsp_static" ]
  output_extension = "so"
  part_name = "ets_frontend"
  subsystem_name = "arkcompiler"
}
