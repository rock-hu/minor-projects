# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_runtime/js_runtime_config.gni")
import("//arkcompiler/ets_runtime/test/test_helper.gni")

module_output_path = "ets_runtime"

test_js_path = "//arkcompiler/ets_runtime/ecmascript/compiler/aot_file/tests/aot_file_test_case/"

test_js_files = [
  "file_size_test",
  "file_size_worker",
]

foreach(file, test_js_files) {
  es2abc_gen_abc("gen_${file}_abc") {
    test_js = "${test_js_path}${file}.js"
    test_abc = "$target_out_dir/${file}.abc"
    extra_visibility = [ ":*" ]
    src_js = rebase_path(test_js)
    dst_file = rebase_path(test_abc)
    extra_args = [
      "--module",
      "--merge-abc",
    ]
    in_puts = [ test_js ]
    out_puts = [ test_abc ]
  }
}

host_unittest_action("AOTFileTest") {
  module_out_path = module_output_path

  sources = [ "aot_file_test.cpp" ]

  configs = [
    "../../../../:asm_interp_enable_config",
    "../../../../:ecma_test_config",
  ]

  deps = [
    "../../:libark_jsoptimizer_set",
    "../../../../:libark_jsruntime_test",
  ]

  external_deps = [
    "icu:shared_icui18n",
    "icu:shared_icuuc",
    "runtime_core:libarkassembler_static",
  ]

  foreach(file, test_js_files) {
    deps += [ ":gen_${file}_abc" ]
  }

  if (is_ohos && is_standard_system) {
    test_abc_dir = "/data/test"
  } else {
    test_abc_dir = rebase_path(target_out_dir)
  }
  defines = [ "TARGET_ABC_PATH=\"${test_abc_dir}/\"" ]
  external_deps += hiviewdfx_ext_deps
  deps += hiviewdfx_deps
}

host_unittest_action("AOTChecksumTest") {
  module_out_path = module_output_path

  sources = [ "aot_checksum_test.cpp" ]

  configs = [
    "../../../../:asm_interp_enable_config",
    "../../../../:ecma_test_config",
  ]

  deps = [
    "../../:libark_jsoptimizer_set",
    "../../../../:libark_jsruntime_test",
  ]

  external_deps = [
    "icu:shared_icui18n",
    "icu:shared_icuuc",
    "runtime_core:libarkassembler_static",
  ]

  external_deps += hiviewdfx_ext_deps
  deps += hiviewdfx_deps
}

group("unittest") {
  testonly = true
  deps = []
  deps += [
    ":AOTChecksumTestAction",
    ":AOTFileTestAction",
  ]
}

group("host_unittest") {
  testonly = true
  deps = [
    ":AOTChecksumTestAction",
    ":AOTFileTestAction",
  ]

  if (is_mac) {
    deps -= [
      ":AOTChecksumTestAction",
      ":AOTFileTestAction",
    ]
  }
}
