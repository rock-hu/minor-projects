# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_runtime/js_runtime_config.gni")
import("//arkcompiler/ets_runtime/test/test_helper.gni")

module_output_path = "ets_runtime"

test_js_path =
    "//arkcompiler/ets_runtime/ecmascript/pgo_profiler/tests/pgo_test_case/"

test_js_files = [
  "op_type_test",
  "class_test",
  "call_test",
  "builtins_test",
  "array_test",
  "object_literal",
  "sample_test",
  "string_equal",
  "array_size_test",
  "truck",
  "vehicle",
  "typedarray_length",
  "merge_file_1",
  "merge_file_2",
  "merge_file_3",
]

foreach(file, test_js_files) {
  es2abc_gen_abc("gen_${file}_abc") {
    test_js = "${test_js_path}${file}.js"
    test_abc = "$target_out_dir/${file}.abc"

    # Only targets in this file can depend on this.
    extra_visibility = [ ":*" ]
    src_js = rebase_path(test_js)
    dst_file = rebase_path(test_abc)
    extra_args = []
    extra_args += [ "--module" ]
    extra_args += [ "--merge-abc" ]
    in_puts = [ test_js ]
    out_puts = [ test_abc ]
  }
}

host_unittest_action("PGOProfilerTest") {
  module_out_path = module_output_path

  sources = [
    # test file
    "pgo_profiler_test.cpp",
  ]

  configs = [
    "//arkcompiler/ets_runtime:asm_interp_enable_config",
    "//arkcompiler/ets_runtime:ecma_test_config",
  ]

  deps = [ "$js_root:libark_jsruntime_test" ]

  external_deps = [
    "icu:shared_icui18n",
    "icu:shared_icuuc",
    "runtime_core:libarkassembler_static",
  ]

  foreach(file, test_js_files) {
    deps += [ ":gen_${file}_abc" ]
  }

  if (is_ohos && is_standard_system) {
    test_abc_dir = "/data/test"
  } else {
    test_abc_dir = rebase_path(target_out_dir)
  }
  defines = [ "TARGET_ABC_PATH=\"${test_abc_dir}/\"" ]

  # hiviewdfx libraries
  external_deps += hiviewdfx_ext_deps
  deps += hiviewdfx_deps
}

host_unittest_action("PGOProfilerTestOne") {
  module_out_path = module_output_path

  sources = [
    # test file
    "pgo_profiler_test_one.cpp",
  ]

  configs = [
    "../../../:asm_interp_enable_config",
    "../../../:ecma_test_config",
  ]

  deps = [ "$js_root:libark_jsruntime_test" ]

  external_deps = [
    "icu:shared_icui18n",
    "icu:shared_icuuc",
    "runtime_core:libarkassembler_static",
  ]

  foreach(file, test_js_files) {
    deps += [ ":gen_${file}_abc" ]
  }

  if (is_ohos && is_standard_system) {
    test_abc_dir = "/data/test"
  } else {
    test_abc_dir = rebase_path(target_out_dir)
  }
  defines = [ "TARGET_ABC_PATH=\"${test_abc_dir}/\"" ]

  # hiviewdfx libraries
  external_deps += hiviewdfx_ext_deps
  deps += hiviewdfx_deps
}

tests = [
  "PGOProfilerTest",
  "PGOProfilerTestOne",
]

group("unittest") {
  testonly = true
  deps = []
  foreach(test, tests) {
    deps += [ ":${test}" ]
  }
}

group("host_unittest") {
  testonly = true
  deps = []
  foreach(test, tests) {
    deps += [ ":${test}Action" ]
  }

  if (is_mac) {
    foreach(test, tests) {
      deps -= [ ":${test}Action" ]
    }
  }
}
