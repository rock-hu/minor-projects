# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

declare_args() {
  is_debug = true
  is_standard_system = false
}

is_linux = current_os == "linux"
is_mingw = current_os == "mingw"
is_mac = current_os == "mac"
is_win = current_os == "win" || current_os == "winuwp"

_shared_binary_target_configs = [ "//build:compiler_defaults" ]
set_defaults("executable") {
  configs = _shared_binary_target_configs
  configs += [ "//build:executable_config" ]
  configs += [ "//build:ldconfig" ]
}
set_defaults("static_library") {
  configs = _shared_binary_target_configs
}
set_defaults("shared_library") {
  configs = _shared_binary_target_configs
  configs += [ "//build:ldconfig" ]
}
set_defaults("source_set") {
  configs = _shared_binary_target_configs
}

host_toolchain = "//build/toolchain:clang-9"
set_default_toolchain(host_toolchain)

ark_standalone_build = true
