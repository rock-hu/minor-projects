# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/libabckit/abckit_config.gni")
import("//arkcompiler/runtime_core/static_core/ark_config.gni")
import("//arkcompiler/runtime_core/static_vm_config.gni")

ark_gen_file("get_intrinsic_id_static_inc") {
  extra_dependencies =
      [ "$ark_root_static/runtime:arkruntime_gen_intrinsics_yaml" ]
  template_file = "templates/get_intrinsic_id_static.inc.erb"
  data = [ "$target_gen_dir/../../../static_core/runtime/intrinsics.yaml" ]
  api = [ "$ark_root_static/runtime/templates/intrinsics.rb" ]
  output_file = "$target_gen_dir/generated/get_intrinsic_id_static.inc"
}

group("abckit_adapter_static_header_deps") {
  deps = [
    ":get_intrinsic_id_static_inc",
    "$abckit_root/src/adapter_dynamic/templates:abckit_isa_gen_inst_props_helpers_dynamic_inc",
    "$abckit_root/src/templates/abckit_intrinsics:abckit_ark_gen_abckit_intrinsics_inl",
    "$abckit_root/src/templates/abckit_intrinsics:abckit_ark_gen_abckit_intrinsics_opcodes_inc",
    "$abckit_root/src/templates/abckit_intrinsics:abckit_ark_gen_abckit_intrinsics_vreg_width_h",
    "$abckit_root/src/templates/dyn_intrinsics:abckit_isa_gen_dyn_intrinsics_enum_inc",
    "$abckit_root/src/templates/dyn_intrinsics:abckit_isa_gen_dyn_intrinsics_flags_inc",
    "$abckit_root/src/templates/dyn_intrinsics:abckit_isa_gen_get_dyn_intrinsics_names_inc",
  ]
}

ohos_source_set("abckit_adapter_static_source_set") {
  sources = [
    "abckit_static.cpp",
    "helpers_static.cpp",
    "ir_static.cpp",
    "ir_static_instr_1.cpp",
    "metadata_inspect_static.cpp",
    "metadata_modify_static.cpp",
    "runtime_adapter_static.cpp",
  ]

  ins_create_wrapper_dyn_dir =
      get_label_info(
          "$abckit_root/wrappers:abckit_isa_gen_ins_create_wrapper_api_inc",
          "target_gen_dir")
  intrinsics_gen_dir = get_label_info(
          "$abckit_root/adapter_dynamic/templates:abckit_isa_gen_dyn_intrinsics_enum_inl",
          "target_gen_dir")
  include_dirs = [
    "$ark_root_dynamic/static_core",  # this target should not include headers
                                      # from dynamic runtime, so static_core
                                      # must be listed first
    "$ark_root_dynamic",
    "$abckit_root",
    "$target_gen_dir/../codegen",
    "$target_gen_dir/../adapter_static",
    "$target_gen_dir/../adapter_dynamic/generated",
    "$target_gen_dir/../libabckit/src",
    "$intrinsics_gen_dir/../generated",
    "$ins_create_wrapper_dyn_dir",
  ]

  part_name = "runtime_core"
  subsystem_name = "arkcompiler"

  configs = [
    "$ark_root_static/assembler:arkassembler_public_config",
    "$ark_root_static/bytecode_optimizer:bytecodeopt_public_config",
    "$ark_root_static/compiler:arkcompiler_public_config",
    "$ark_root_static/libpandafile:arkfile_public_config",
    "$ark_root_static/libpandabase:arkbase_public_config",
    "$ark_root_static/runtime:arkruntime_public_config",
    "$ark_root_dynamic/static_core:ark_config",
    "$abckit_root:abckit_config",
  ]

  deps = [
    ":abckit_adapter_static_header_deps",
    "$abckit_root/src:abckit_concat_intrinsics_yaml",
    "$ark_root_static/abc2program:arkts_abc2program",
    "$ark_root_static/assembler:libarktsassembler",
    "$ark_root_static/assembler:libarktsassembler",
    "$ark_root_static/bytecode_optimizer:libarktsbytecodeopt",
    "$ark_root_static/bytecode_optimizer:libarktsbytecodeopt",
    "$ark_root_static/libpandabase:libarktsbase",
    "$ark_root_static/libpandafile:libarktsfile",
  ]
}
