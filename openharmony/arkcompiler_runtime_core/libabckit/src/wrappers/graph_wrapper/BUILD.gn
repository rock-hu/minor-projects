# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/libabckit/libabckit_config.gni")
import("//arkcompiler/runtime_core/static_core/ark_config.gni")
import("//arkcompiler/runtime_core/static_vm_config.gni")

ohos_source_set("libabckit_graph_wrapper_source_set") {
  sources = [ "graph_wrapper.cpp" ]

  cross_values_gen_dir = get_label_info(
          "$ark_root/cross_values:cross_values_getters_generate(${default_toolchain})",
          "target_gen_dir")
  entrypoints_compiler_gen_dir = get_label_info(
          "$ark_root/runtime:arkruntime_gen_entrypoints_entrypoints_compiler_inl",
          "target_gen_dir")
  bytecode_inst_enum_gen_dir = get_label_info(
          "$ark_root/../libabckit/src/irbuilder_dynamic/templates:isa_gen_libabckit_bytecode_inst_enum_gen_h",
          "target_gen_dir")
  intrinsics_gen_dir = get_label_info(
          "$ark_root/../libabckit/src/templates/dyn_intrinsics:isa_gen_libabckit_dyn_intrinsics_enum_inl",
          "target_gen_dir")
  reg_encoder_visitors_dir =
      get_label_info("$ark_root/bytecode_optimizer:reg_encoder_visitors_inc",
                     "target_gen_dir")
  codegen_visitors_dyn_dir = get_label_info(
          "$ark_root/../libabckit/src/codegen/templates_dynamic:codegen_visitors_dyn_inc",
          "target_gen_dir")
  ins_create_wrapper_dyn_dir = get_label_info(
          "$ark_root/../libabckit/src/wrappers:isa_gen_libabckit_ins_create_wrapper_api_inc",
          "target_gen_dir")
  include_dirs = [
    "$ark_root/runtime/libpandabase",
    "$ark_root/runtime",
    "$entrypoints_compiler_gen_dir/generated",
    "$bytecode_inst_enum_gen_dir/..",
    "$intrinsics_gen_dir/../generated",
    "$reg_encoder_visitors_dir",
    "$codegen_visitors_dyn_dir/..",
    "$cross_values_gen_dir",
    "$ins_create_wrapper_dyn_dir",
    "$ark_root",  # this target should not include headers from dynamic runtime,
                  # so static_core must be listed first
    "$ark_root/..",
  ]

  part_name = "runtime_core"
  subsystem_name = "arkcompiler"

  configs = [
    "$ark_root/:ark_config",
    "$ark_root/assembler:arkassembler_public_config",
    "$ark_root/compiler:arkcompiler_public_config",
    "$ark_root/libpandabase:arkbase_public_config",
    "$ark_root/libpandafile:arkfile_public_config",
    "$ark_root/runtime:profiling_gen_public_config",
    "$abckit_root:libabckit_coverage",
    "$abckit_root:libabckit_config",
  ]

  deps = [ "$ark_root/compiler:libarktscompiler" ]
}
