# Copyright (c) 2021-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/ark_config.gni")

config("arkziparchive_config") {
  include_dirs = [
    "$ark_root/libziparchive",
    "$ark_root/libpandabase",
  ]
}

ohos_static_library("libarkziparchive_static") {
  sources = [ "zip_archive.cpp" ]

  public_configs = [ ":arkziparchive_config" ]
  configs = [
    "$ark_root:ark_config",
    "$ark_root/libpandabase:arkbase_public_config",
  ]

  deps = [ "$ark_root/libpandabase:libarkbase_static" ]

  if (is_arkui_x) {
    deps += [
      "$ark_third_party_root/bounds_checking_function:libsec_static",
      "$ark_third_party_root/zlib:libz",
    ]
  } else if (ark_standalone_build) {
    external_deps = [
      "zlib:shared_libz",
      sdk_libc_secshared_dep,
    ]
  } else {
    public_external_deps = [
      "zlib:shared_libz",
      sdk_libc_secshared_dep,
    ]
  }

  if (enable_hilog) {
    public_external_deps += [ "hilog:libhilog" ]
  }

  part_name = "runtime_core"
  subsystem_name = "arkcompiler"
}

ohos_shared_library("libarkziparchive") {
  stack_protector_ret = false
  deps = [ ":libarkziparchive_static" ]

  if (!is_standard_system) {
    relative_install_dir = "ark"
  }
  if (!is_mingw && !is_mac) {
    output_extension = "so"
  }

  part_name = "runtime_core"
  subsystem_name = "arkcompiler"
}

ohos_source_set("libarkziparchive_frontend_set_static") {
  sources = [ "zip_archive.cpp" ]

  public_configs = [
    ":arkziparchive_config",
    "$ark_root:ark_config",
    "$ark_root/libpandabase:arkbase_public_config",
  ]

  deps = [ "$ark_root/libpandabase:libarkbase_frontend_static" ]

  if (is_arkui_x) {
    deps += [
      "$ark_third_party_root/bounds_checking_function:libsec_static",
      "$ark_third_party_root/zlib:libz",
    ]
  } else {
    external_deps = [
      "zlib:libz",
      sdk_libc_secshared_dep,
    ]
    if (enable_hilog) {
      external_deps += [ "hilog:libhilog" ]
    }
  }

  part_name = "runtime_core"
  subsystem_name = "arkcompiler"
}

ohos_static_library("libarkziparchive_frontend_static") {
  stack_protector_ret = false
  if (defined(ohos_indep_compiler_enable) && ohos_indep_compiler_enable) {
    deps = [ ":libarkziparchive_static" ]
  } else {
    deps = [ ":libarkziparchive_frontend_set_static" ]
  }

  part_name = "runtime_core"
  subsystem_name = "arkcompiler"
}
