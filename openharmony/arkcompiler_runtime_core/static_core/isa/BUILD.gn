# Copyright (c) 2021-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/static_core/ark_config.gni")
import("$ark_root/plugins/plugins.gni")

isa_paths = [ "$ark_root/isa/isa.yaml" ] + plugin_isa_paths

action("isa_combine") {
  script = "$ark_root/isa/combine.rb"
  sources = isa_paths
  relative_isa_paths = rebase_path(isa_paths, root_build_dir)
  input_args = string_join(",", relative_isa_paths)
  outputs = [ "$ark_isa_yaml" ]
  args = [
    "-d",
    input_args,
    "-o",
    rebase_path(outputs[0], root_build_dir),
  ]
}

ohos_prebuilt_etc("isa_combine_static_etc") {
  isa_combine_outputs = get_target_outputs(":isa_combine")
  source = isa_combine_outputs[0]
  deps = [ ":isa_combine" ]
  install_enable = false
  part_name = ark_part_name
  subsystem_name = ark_subsystem_name
}
