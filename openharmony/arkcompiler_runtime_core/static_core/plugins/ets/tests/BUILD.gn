# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/static_core/ark_config.gni")

nproc = getenv("NPROC_PER_JOB")
if (nproc == "") {
  nproc = 12
}

action("ets_func_tests_int_jit_aot") {
  script = "/bin/bash"

  outputs = [ "$target_out_dir/ets_func_tests" ]

  deps = [
    "$ark_root/compiler/tools/paoc:ark_aot",
    "$ark_root/panda:arkts_bin",
    "$ark_root/plugins/ets:ets-compile-stdlib-default",
    "$ark_root/plugins/ets:etsstdlib",
    "$ark_root/verification/verifier:verifier_bin",
  ]

  if (ark_standalone_build || ark_static_standalone_build) {
    deps += [ "$ark_es2panda_root/aot:ets2panda" ]
  } else {
    external_deps = [ "ets_frontend:ets2panda(${host_toolchain})" ]
  }

  runner_path = rebase_path("$ark_root/tests/tests-u-runner/runner.sh")

  args_common = [
    "--ets-func-tests",
    "--build-dir=" + rebase_path(root_build_dir),
    "--force-generate",
    "--es2panda-opt-level=2",
    "--gc-type=g1-gc",
    "--processes=$nproc",
  ]

  args_int = [
    "--timeout=30",
    "--heap-verifier=fail_on_verification:pre:into:before_g1_concurrent:post",
  ]

  args_jit = [
    "--timeout=90",
    "--heap-verifier=fail_on_verification:pre:into:post",
    "--jit",
    "--ark-args=--no-async-jit=true",
    "--ark-args=--compiler-ignore-failures=false",
  ]

  args_aot = [
    "--timeout=30",
    "--heap-verifier=fail_on_verification:pre:into:before_g1_concurrent:post",
    "--aot",
    "--ark-args=--compiler-check-final=true",
    "--ark-args=--compiler-ignore-failures=false",
    "--ark-args=--enable-an:force",
  ]

  string_args_int = string_join(" ", args_common + args_int)
  string_args_jit = string_join(" ", args_common + args_jit)
  string_args_aot = string_join(" ", args_common + args_aot)

  command_str = "$runner_path $string_args_int;$runner_path $string_args_jit;$runner_path $string_args_aot;"
  if (is_asan) {
    command_str = "$runner_path $string_args_int;"
  }
  args = [
    "-c",
    "$command_str",
  ]
}
