# Copyright (c) 2022-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/static_core/ark_config.gni")

libarkinspector_sources = [
  "connection/endpoint_base.cpp",
  "connection/event_loop.cpp",
  "connection/ohos_ws/ohos_ws_server.cpp",
  "connection/ohos_ws/ohos_ws_server_endpoint.cpp",
  "connection/server_endpoint_base.cpp",
  "debug_info_cache.cpp",
  "debuggable_thread.cpp",
  "error.cpp",
  "evaluation/conditions_storage.cpp",
  "evaluation/evaluation_engine.cpp",
  "init.cpp",
  "inspector.cpp",
  "inspector_server.cpp",
  "json_serialization/jrpc_error.cpp",
  "object_repository.cpp",
  "session_manager.cpp",
  "source_manager.cpp",
  "thread_state.cpp",
  "types/custom_url_breakpoint_response.cpp",
  "types/debugger_call_function_on_request.cpp",
  "types/debugger_evaluation_request.cpp",
  "types/exception_details.cpp",
  "types/location.cpp",
  "types/object_preview.cpp",
  "types/remote_object.cpp",
  "types/scope.cpp",
  "types/url_breakpoint_request.cpp",
  "types/url_breakpoint_response.cpp",
]

config("libarkinspector_config") {
  cflags_cc = [
    "-fexceptions",
    "-Wno-invalid-offsetof",
    "-Wno-null-pointer-subtraction",
    "-Wno-error=unknown-warning-option",
  ]

  # required for arkcompiler_toolchain compilation
  if (is_ohos) {
    defines = [ "OHOS_PLATFORM" ]
  }
}

ohos_shared_library("libarkinspector") {
  configs = [
    "$ark_root:ark_config",
    "$ark_root/compiler:arkcompiler_public_config",
    "$ark_root/disassembler:arkdisassembler_public_config",
    "$ark_root/libpandabase:arkbase_public_config",
    "$ark_root/libpandafile:arkfile_public_config",
    "$ark_root/runtime:arkruntime_public_config",
    ":libarkinspector_config",
  ]
  external_deps = [
    "toolchain:libwebsocket_server",
    sdk_libc_secshared_ext_dep,
  ]
  deps = [
    "$ark_root/assembler:libarktsassembler",
    "$ark_root/compiler:libarktscompiler",
    "$ark_root/disassembler:arktsdisassembler",
    "$ark_root/libpandabase:libarktsbase",
    "$ark_root/libpandafile:libarktsfile",
    "$ark_root/runtime:libarkruntime",
  ]
  sources = libarkinspector_sources
  include_dirs = [
    "$ark_root/runtime/include",
    "$ark_root/runtime/tooling/inspector",
    get_label_info("$ark_root/assembler:isa_gen_libarkassembler",
                   "target_gen_dir"),
    get_label_info("$ark_root/disassembler:disasm_plugins_inc",
                   "target_gen_dir"),
  ]

  external_deps += hiviewdfx_ext_deps
  if (defined(ohos_indep_compiler_enable) && ohos_indep_compiler_enable) {
    external_deps += [ "openssl:libcrypto_shared" ]
  }
  if (is_mingw || is_win) {
    output_extension = "dll"
  } else {
    output_extension = "so"
  }
  part_name = ark_part_name
  subsystem_name = "$ark_subsystem_name"
}
