# Copyright (c) 2021-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/static_core/ark_config.gni")
if (ark_standalone_build) {
  import("$build_root/ark.gni")
} else {
  import("//build/ohos.gni")
}

config("verification_public_config") {
  include_dirs = [
    "$ark_root/verification",
    "$target_gen_dir",
    "$target_gen_dir/include",
    "$target_gen_dir/../..",
  ]
}

if (!ark_static_standalone_build) {
  ohos_shared_headers("verification_headers") {
    include_dirs = [
      "$ark_root/verification",
      "$target_gen_dir",
      "$target_gen_dir/include",
      "$target_gen_dir/../..",
    ]
    part_name = ark_part_name
    subsystem_name = "$ark_subsystem_name"
  }
}

ark_gen_file("verification_abs_int_inl_compat_checks_h") {
  template_file = "templates/abs_int_inl_compat_checks.h.erb"
  data = [ "$ark_root/verification/verification.yaml" ]
  api = [ "$ark_root/verification/verification.rb" ]
  output_file = "$target_gen_dir/include/abs_int_inl_compat_checks.h"
}

ark_isa_gen("isa_gen_verification_gen") {
  template_files = [
    "cflow_iterate_inl_gen.h.erb",
    "abs_int_inl_gen.h.erb",
    "job_fill_gen.h.erb",
    "handle_gen.h.erb",
  ]
  sources = "templates"  ## ark_root/templates
  destination = "$target_gen_dir/include"  ## target_gen_dir/include
  extra_dependencies = [ ":verification_abs_int_inl_compat_checks_h" ]
}

ark_gen_file("verification_verifier_messages_h") {
  template_file = "$ark_root/templates/messages/messages.h.erb"
  data = [ "$ark_root/verification/messages.yaml" ]
  api = [ "$ark_root/templates/messages.rb" ]
  output_file = "$target_gen_dir/include/verifier_messages.h"
}

ark_gen_file("verification_gen_plugins_gen_inc") {
  extra_dependencies = [ "$ark_root:concat_plugins_yamls" ]
  template_file = "$ark_root/verification/gen/templates/plugins_gen.inc.erb"
  data = [ ark_plugin_options_yaml ]
  api = [ "$ark_root/templates/plugin_options.rb" ]
  output_file = "$target_gen_dir/include/plugins_gen.inc"
}

ark_gen_file("verification_verifier_messages_data_cpp") {
  template_file =
      "$ark_root/verification/gen/templates/verifier_messages_data_gen.cpp.erb"
  data = [ "$ark_root/verification/messages.yaml" ]
  api = [ "$ark_root/templates/messages.rb" ]
  output_file = "$target_gen_dir/../verifier_messages_data_gen.cpp"
}
