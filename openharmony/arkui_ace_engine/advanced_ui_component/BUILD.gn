# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("advanced_ui_component.gni")

config("ace_napi_config") {
  include_dirs = [
    "$ace_root",
    "$arkui_root",
    "$ace_root/frameworks",
    "$root_out_dir/arkui",
    "$root_out_dir/arkui/framework",
  ]
  cflags_cc = []
  defines = []
  if (use_mingw_win) {
    cflags_cc += [ "-std=c++17" ]
    defines += [ "_USE_MATH_DEFINES" ]
  }

  if (use_linux) {
    cflags_cc += [ "-std=c++17" ]
    defines += [ "_USE_MATH_DEFINES" ]
  }

  if (is_mac) {
    cflags_cc = [ "-O0" ]
  }
}

advanced_ui_component_targets = []
foreach(module, advanced_ui_component_libs) {
  if (use_mac) {
    advanced_ui_component_lib(module) {
      platform = "mac"
    }
  } else if (use_mingw_win) {
    advanced_ui_component_lib(module) {
      platform = "windows"
    }
  } else if (use_linux) {
    advanced_ui_component_lib(module) {
      platform = "linux"
    }
  } else if (!is_arkui_x) {
    advanced_ui_component_lib(module) {
      # napi libs remain isolate in libace.z.so and asan version
      if (is_asan) {
        platform = "ohos"
      } else {
        platform = "ohos_ng"
      }
    }
  }

  advanced_ui_component_targets += [ ":$module" ]
}

group("advanced_ui_component") {
  deps = advanced_ui_component_targets
  deps += [
    "chipgroup/interfaces:chipgroup",
    "composelistitem/interfaces:composelistitem",
    "composetitlebar/interfaces:composetitlebar",
    "counter/interfaces:counter",
    "dialog/interfaces:dialog",
    "downloadfilebutton/interfaces:downloadfilebutton",
    "editabletitlebar/interfaces:editabletitlebar",
    "exceptionprompt/interfaces:exceptionprompt",
    "filter/interfaces:filter",
    "foldsplitcontainer/interfaces:foldsplitcontainer",
    "formmenu/interfaces:formmenu",
    "fullscreenlaunchcomponent/interfaces:fullscreenlaunchcomponent",
    "gridobjectsortcomponent/interfaces:gridobjectsortcomponent",
    "multinavigation/interfaces:multinavigation",
    "navpushpathhelper:navpushpathhelper",
    "popup/interfaces:popup",
    "progressbutton/interfaces:progressbutton",
    "segmentbutton/interfaces:segmentbutton",
    "selectionmenu/interfaces:selectionmenu",
    "selecttitlebar/interfaces:selecttitlebar",
    "splitlayout/interfaces:splitlayout",
    "subheader/interfaces:subheader",
    "swiperefresher/interfaces:swiperefresher",
    "tabtitlebar/interfaces:tabtitlebar",
    "toolbar/interfaces:toolbar",
    "treeview/interfaces:treeview",
  ]
}
