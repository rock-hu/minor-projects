# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

advanced_ui_component_libs = [
  "chip",
  "segmentbuttonv2",
]

template("advanced_ui_component_lib") {
  forward_variables_from(invoker, "*")

  ohos_shared_library(target_name) {
    if (current_os == "ohos") {
      sanitize = {
        integer_overflow = true
        boundary_sanitize = true
        debug = ace_sanitize_debug
      }
    }
    deps = [ "$ace_root/advanced_ui_component/${target_name}/interfaces:${target_name}_static_${platform}" ]
    cflags = []
    ldflags = []
    sources = []
    defines = []
    include_dirs = []
    if (ace_engine_feature_enable_coverage) {
      cflags += [ "--coverage" ]
      ldflags += [ "--coverage" ]
    }
    if (use_hilog) {
      external_deps = [ "hilog:libhilog" ]
    }
    relative_install_dir = "module/arkui/advanced"
    part_name = ace_engine_part
    subsystem_name = ace_engine_subsystem
  }
}
