# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/adapter/preview/build/config.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

es2abc_gen_abc("gen_exceptionprompt_abc") {
  src_js = rebase_path("exceptionprompt.js")
  dst_file = rebase_path(target_out_dir + "/exceptionprompt.abc")
  in_puts = [ "exceptionprompt.js" ]
  out_puts = [ target_out_dir + "/exceptionprompt.abc" ]
  extra_args = [ "--module" ]
}

gen_js_obj("exceptionprompt_abc") {
  input = get_label_info(":gen_exceptionprompt_abc", "target_out_dir") +
          "/exceptionprompt.abc"
  output = target_out_dir + "/exceptionprompt_abc.o"
  dep = ":gen_exceptionprompt_abc"
}

gen_obj("exceptionprompt_abc_preview") {
  input = get_label_info(":gen_exceptionprompt_abc", "target_out_dir") +
          "/exceptionprompt.abc"
  output = target_out_dir + "/exceptionprompt_abc.c"
  snapshot_dep = [ ":gen_exceptionprompt_abc" ]
}

ohos_shared_library("exceptionprompt") {
  sources = [ "exceptionprompt.cpp" ]

  if (use_mingw_win || use_mac || use_linux) {
    deps = [ ":gen_obj_src_exceptionprompt_abc_preview" ]
  } else {
    deps = [ ":exceptionprompt_abc" ]
  }

  external_deps = [
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  relative_install_dir = "module/arkui/advanced"
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}
