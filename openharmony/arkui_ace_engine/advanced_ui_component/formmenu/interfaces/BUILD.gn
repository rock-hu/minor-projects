# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/adapter/preview/build/config.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

es2abc_gen_abc("gen_formmenu_abc") {
  if (ace_engine_feature_form_menu_enable) {
    src_js = rebase_path("formmenu.js")
    dst_file = rebase_path(target_out_dir + "/formmenu.abc")
    in_puts = [ "formmenu.js" ]
    out_puts = [ target_out_dir + "/formmenu.abc" ]
    extra_args = [ "--module" ]
  } else {
    src_js = rebase_path("formmenu_wear.js")
    dst_file = rebase_path(target_out_dir + "/formmenu.abc")
    in_puts = [ "formmenu_wear.js" ]
    out_puts = [ target_out_dir + "/formmenu.abc" ]
    extra_args = [ "--module" ]
  }
}

gen_js_obj("formmenu_abc") {
  input =
      get_label_info(":gen_formmenu_abc", "target_out_dir") + "/formmenu.abc"
  output = target_out_dir + "/formmenu_abc.o"
  dep = ":gen_formmenu_abc"
}

es2abc_gen_abc("gen_formmenu_abc_preview") {
  src_js = rebase_path("formmenu_preview.js")
  dst_file = rebase_path(target_out_dir + "/formmenu_preview.abc")
  in_puts = [ "formmenu_preview.js" ]
  out_puts = [ target_out_dir + "/formmenu_preview.abc" ]
  extra_args = [ "--module" ]
}

gen_obj("formmenu_abc_preview") {
  input = get_label_info(":gen_formmenu_abc_preview", "target_out_dir") +
          "/formmenu_preview.abc"
  output = target_out_dir + "/formmenu_abc_preview.c"
  snapshot_dep = [ ":gen_formmenu_abc_preview" ]
}

ohos_shared_library("formmenu") {
  if (use_mingw_win || use_mac || use_linux) {
    deps = [ ":gen_obj_src_formmenu_abc_preview" ]
    sources = [ "formmenu_preview.cpp" ]
  } else {
    deps = [ ":formmenu_abc" ]
    sources = [ "formmenu.cpp" ]
  }

  external_deps = [
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  relative_install_dir = "module/arkui/advanced"
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}
