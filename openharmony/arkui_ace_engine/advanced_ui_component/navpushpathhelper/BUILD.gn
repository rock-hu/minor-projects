# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/adapter/preview/build/config.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

es2abc_gen_abc("gen_navpushpathhelper_abc") {
  src_js = rebase_path("navpushpathhelper.js")
  dst_file = rebase_path(target_out_dir + "/navpushpathhelper.abc")
  in_puts = [ "navpushpathhelper.js" ]
  out_puts = [ target_out_dir + "/navpushpathhelper.abc" ]
  extra_args = [ "--module" ]
}

gen_js_obj("navpushpathhelper_abc") {
  input = get_label_info(":gen_navpushpathhelper_abc", "target_out_dir") +
          "/navpushpathhelper.abc"
  output = target_out_dir + "/navpushpathhelper_abc.o"
  dep = ":gen_navpushpathhelper_abc"
}

gen_obj("navpushpathhelper_abc_preview") {
  input = get_label_info(":gen_navpushpathhelper_abc", "target_out_dir") +
          "/navpushpathhelper.abc"
  output = target_out_dir + "/navpushpathhelper_abc.c"
  snapshot_dep = [ ":gen_navpushpathhelper_abc" ]
}

additional_include_dirs = [ "${ace_root}" ]

ohos_shared_library("navpushpathhelper") {
  sources = [
    "src/hsp_silentinstall.cpp",
    "src/hsp_silentinstall_napi.cpp",
    "src/navpushpathhelper.cpp",
  ]

  if (use_mingw_win || use_mac || use_linux) {
    deps = [ ":gen_obj_src_navpushpathhelper_abc_preview" ]
  } else {
    deps = [ ":navpushpathhelper_abc" ]
  }

  include_dirs = additional_include_dirs
  include_dirs += [ "${ace_root}/frameworks" ]
  deps += [ "$ace_root/build:libace_compatible" ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:abilitykit_native",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "napi:ace_napi",
    "samgr:samgr_proxy",
  ]

  relative_install_dir = "module/atomicservice"
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}
