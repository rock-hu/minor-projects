# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/es2panda/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

component_ext_path = "${ace_root}/component_ext"

additional_include_dirs = [ "${ace_root}" ]

es2abc_gen_abc("gen_arkui_arcswiper_abc") {
  src_js = rebase_path("arkui_arcswiper.js")
  dst_file = rebase_path(target_out_dir + "/arkui_arcswiper.abc")
  in_puts = [ "arkui_arcswiper.js" ]
  out_puts = [ target_out_dir + "/arkui_arcswiper.abc" ]
  extra_args = [ "--module" ]
}

gen_obj("arkui_arcswiper_abc") {
  input = get_label_info(":gen_arkui_arcswiper_abc", "target_out_dir") +
          "/arkui_arcswiper.abc"
  output = target_out_dir + "arkui_arcswiper_abc.o"
  if (use_mac || use_mingw_win || use_linux || target_os == "ios") {
    output = target_out_dir + "arkui_arcswiper_abc.c"
  }
  snapshot_dep = [ ":gen_arkui_arcswiper_abc" ]
}

ohos_shared_library("arcswiper") {
  defines = [
    "USE_ARK_ENGINE",
    "NOGDI",
  ]
  sources = [
    "${component_ext_path}/arc_swiper/arc_swiper_napi.cpp",
    "${component_ext_path}/ext_common/ext_napi_utils.cpp",
  ]

  include_dirs = [
    "${component_ext_path}/ext_common/",
    "${component_ext_path}/arc_swiper/",
    "${ace_root}/frameworks",
    "$root_out_dir/arkui/framework",
  ]
  include_dirs += additional_include_dirs

  deps = [
    ":gen_obj_src_arkui_arcswiper_abc",
    "$ace_root/build:libace_compatible",
    "$ace_root/frameworks/core:ace_container_scope",
    "$ace_root/interfaces/inner_api/ace:ace_uicontent",
  ]

  external_deps = [
    "bounds_checking_function:libsec_static",
    "ets_runtime:libark_jsruntime",
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  if (!use_mac) {
    ldflags = [ "-Wl,--gc-sections" ]
  }

  cflags = [
    "-fvisibility=hidden",
    "-fdata-sections",
    "-ffunction-sections",
    "-Os",
  ]

  cflags_cc = [
    "-fvisibility-inlines-hidden",
    "-Os",
  ]

  defines += [ "_USE_MATH_DEFINES" ]

  relative_install_dir = "module/arkui"
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}
