# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos_var.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

template("ace_base_base64") {
  forward_variables_from(invoker, "*")
  source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc
    if (is_arkui_x) {
      deps = [ "//third_party/skia:skia_canvaskit_static" ]
    } else {
      external_deps = [ "skia:skia_canvaskit" ]
    }
    if (use_new_skia) {
      defines += [ "USE_NEW_SKIA" ]
    }

    # add base source file here
    sources = [ "base64_util.cpp" ]

    configs += [ "$ace_root:ace_config" ]
  }
}

foreach(item, ace_platforms) {
  ace_base_base64("ace_base_base64_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
