# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import(
    "//foundation/arkui/ace_engine/frameworks/core/components_ng/components.gni")

build_component_ng("base_ng") {
  sources = [
    "distributed_ui.cpp",
    "extension_handler.cpp",
    "frame_node.cpp",
    "geometry_node.cpp",
    "inspector.cpp",
    "inspector_filter.cpp",
    "modifier.cpp",
    "node_render_status_monitor.cpp",
    "observer_handler.cpp",
    "simplified_inspector.cpp",
    "transparent_node_detector.cpp",
    "ui_node.cpp",
    "view_abstract.cpp",
    "view_abstract_model_ng.cpp",
    "view_full_update_model_ng.cpp",
    "view_partial_update_model_ng.cpp",
    "view_stack_processor.cpp",
  ]
  if (ace_engine_feature_enable_split_mode &&
      defined(vendor_configs.ace_engine_advanced_split_mode)) {
    sources += vendor_configs.ace_engine_advanced_split_mode
  } else if (ace_engine_enable_circle_feature &&
             defined(vendor_configs.ace_engine_advanced_router_circle_mode)) {
    sources += vendor_configs.ace_engine_advanced_router_circle_mode
  } else if (ace_engine_feature_wearable &&
             defined(vendor_configs.ace_engine_advanced_wearable_pattern)) {
    sources += vendor_configs.ace_engine_advanced_wearable_pattern
  } else {
    sources += [ "view_advanced_register.cpp" ]
  }
  if (!use_mingw_win && !use_mac && !use_linux) {
    external_deps = [ "window_manager:scene_session" ]
  }
  if (ace_engine_feature_enable_nav_split_mode &&
      defined(vendor_configs.ace_engine_advanced_navigation_split_mode)) {
    sources += vendor_configs.ace_engine_advanced_navigation_split_mode
  } else if (ace_engine_enable_circle_feature &&
             defined(
                 vendor_configs.ace_engine_advanced_navigation_circle_mode)) {
    sources += vendor_configs.ace_engine_advanced_navigation_circle_mode
  } else {
    sources += [ "navigation_register.cpp" ]
  }
}
