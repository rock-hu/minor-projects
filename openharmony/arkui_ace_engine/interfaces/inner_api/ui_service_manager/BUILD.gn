# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import(
    "//foundation/arkui/ace_engine/adapter/ohos/services/uiservice/uiservicems.gni")

idl_gen_interface("uiservice_manager_interface") {
  sources = [
    "IUIServiceMgrNew.idl",
    "IUIServiceNew.idl",
  ]
  log_domainid = "0xD003935"
  log_tag = "AceUIService"
  subsystem_name = "AceUIService"
}

config("uiservice_manager_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "$ace_root/frameworks",
    "include/",
    "native/include",
    "${target_gen_dir}",
  ]
  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (ace_engine_enable_uiservice_idl) {
    defines = [ "UI_SERVICE_WITH_IDL" ]
  }
}

ohos_shared_library("ui_service_mgr") {
  if (current_os == "ohos") {
    sanitize = {
      integer_overflow = true
      boundary_sanitize = true
      debug = ace_sanitize_debug
    }
  }

  sources = [
    "$ace_root/adapter/ohos/services/uiservice/src/ui_service_mgr_client.cpp",
    "$ace_root/adapter/ohos/services/uiservice/src/ui_service_mgr_client_idl.cpp",
    "$ace_root/adapter/ohos/services/uiservice/src/ui_service_mgr_proxy.cpp",
    "$ace_root/adapter/ohos/services/uiservice/src/ui_service_mgr_xcollie.cpp",
    "$ace_root/adapter/ohos/services/uiservice/src/ui_service_proxy.cpp",
    "$ace_root/adapter/ohos/services/uiservice/src/ui_service_stub.cpp",
  ]
  output_values = get_target_outputs(":uiservice_manager_interface")
  sources += filter_include(output_values, [ "*_proxy.cpp" ])
  sources += filter_include(output_values, [ "*_stub.cpp" ])

  deps = [ ":uiservice_manager_interface" ]

  configs = [
    "$ace_root:ace_config",
    "$ace_root:ace_coverage_config",
  ]

  public_configs = [
    ":uiservice_manager_public_config",
    "$ace_root/adapter/ohos/services/uiservice:uiservicems_config",
  ]

  external_deps = [
    "ability_base:want",
    "access_token:libtokenid_sdk",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "window_manager:libwm",
  ]

  defines = []

  if (uiservice_xcollie_enable) {
    external_deps += [ "hicollie:libhicollie" ]
    defines += [ "UISERVICE_MGR_XCOLLIE_ENABLE" ]
  }

  subsystem_name = ace_engine_subsystem
  innerapi_tags = [ "platformsdk_indirect" ]
  part_name = ace_engine_part
}

ohos_source_set("uiservice_manager_interface_set") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":uiservice_manager_public_config" ]
  output_values = get_target_outputs(":uiservice_manager_interface")
  sources = filter_include(output_values, [ "*_proxy.cpp" ])
  sources += filter_include(output_values, [ "*_stub.cpp" ])
  deps = [ ":uiservice_manager_interface" ]
  external_deps = [
    "ability_base:want",
    "access_token:libtokenid_sdk",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "window_manager:libwm",
  ]
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part
}
