# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

config("xcomponent_controller_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "$ace_root/frameworks",
    "$ace_root/interfaces/inner_api/xcomponent_controller",
  ]
  defines = [ "USE_ARK_ENGINE" ]
  if (use_hilog) {
    defines += [ "USE_HILOG" ]
  }
  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
}

ohos_shared_library("ace_xcomponent_controller") {
  if (current_os == "ohos") {
    sanitize = {
      integer_overflow = true
      boundary_sanitize = true
      debug = ace_sanitize_debug
    }
  }
  sources = [ "$ace_root/interfaces/inner_api/xcomponent_controller/xcomponent_controller.cpp" ]

  configs = [
    "$ace_root:ace_config",
    "$ace_root:ace_coverage_config",
  ]

  public_configs = [ ":xcomponent_controller_config" ]

  deps = [
    "$ace_root/frameworks/core/components/theme:build_theme_code",
    "$ace_root/interfaces/inner_api/ace:ace_uicontent",
  ]

  external_deps = [
    "c_utils:utils",
    "ets_runtime:libark_jsruntime",
    "napi:ace_napi",
  ]
  public_external_deps = [ "hilog:libhilog" ]

  subsystem_name = ace_engine_subsystem
  innerapi_tags = [ "platformsdk_indirect" ]
  part_name = ace_engine_part
}
