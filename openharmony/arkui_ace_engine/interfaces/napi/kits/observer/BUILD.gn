# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

template("napi_observer_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    defines += ace_common_defines
    cflags_cc += invoker.cflags_cc
    configs = [ "$ace_root/interfaces/napi/kits:ace_napi_config" ]

    sources = [
      "gesture/gesture_observer.cpp",
      "gesture/gesture_observer_listener.cpp",
      "js_ui_observer.cpp",
      "ui_observer.cpp",
      "ui_observer_listener.cpp",
    ]
    deps = []
    external_deps = []
    if (platform != "" && is_arkui_x) {
      deps += [ "${crossplatform_plugin_root}/libs/napi:napi_$platform" ]
    } else {
      external_deps += [ "napi:ace_napi" ]
    }
    if (use_hilog) {
      external_deps += [ "hilog:libhilog" ]
    }
    if (defined(config.libace_target)) {
      deps += [ config.libace_target ]
    }

    deps += [ "../utils:napi_utils_static_${platform}" ]

    if (is_arkui_x) {
      deps += [ "$ace_root/frameworks/core:ace_container_scope_static" ]
    } else {
      deps += [
        "$ace_root/build:libace",
        "$ace_root/frameworks/core:ace_container_scope",
      ]
    }

    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
  }
}

foreach(item, ace_platforms) {
  napi_observer_static("observer_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }
    platform = item.name

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
