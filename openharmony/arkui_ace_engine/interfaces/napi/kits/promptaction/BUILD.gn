# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/adapter/preview/build/config.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

es2abc_gen_abc("gen_promptaction_abc") {
  src_js = rebase_path("promptaction.js")
  dst_file = rebase_path(target_out_dir + "/promptaction.abc")
  in_puts = [ "promptaction.js" ]
  out_puts = [ target_out_dir + "/promptaction.abc" ]
  extra_args = [ "--module" ]
}

base_output_path = get_label_info(":gen_promptaction_abc", "target_out_dir")
promptaction_obj_path = base_output_path + "/promptaction.o"
promptaction_abc_obj_path = base_output_path + "/promptaction_abc.o"

gen_obj("promptaction_js") {
  input = "promptaction.js"
  if (use_mac || use_mingw_win || use_linux || target_os == "ios") {
    promptaction_obj_path = base_output_path + "/promptaction.c"
  }
  output = promptaction_obj_path
  snapshot_dep = []
}

gen_obj("promptaction_abc") {
  input = get_label_info(":gen_promptaction_abc", "target_out_dir") +
          "/promptaction.abc"
  if (use_mac || use_mingw_win || use_linux || target_os == "ios") {
    promptaction_abc_obj_path = base_output_path + "/promptaction_abc.c"
  }
  output = promptaction_abc_obj_path
  snapshot_dep = [ ":gen_promptaction_abc" ]
}

template("napi_promptaction_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    configs = [ "$ace_root/interfaces/napi/kits:ace_napi_config" ]

    sources = [
      "../promptaction/prompt_action.cpp",
      "../promptaction/prompt_controller.cpp",
      "js_level_order.cpp",
      "js_prompt_action.cpp",
      "js_prompt_controller.cpp",
    ]
    deps = [
      ":gen_obj_src_promptaction_abc",
      ":gen_obj_src_promptaction_js",
    ]
    external_deps = []
    if (platform != "" && is_arkui_x) {
      deps += [ "//plugins/libs/napi:napi_$platform" ]
    } else {
      external_deps += [ "napi:ace_napi" ]
    }
    if (use_hilog) {
      external_deps += [ "hilog:libhilog" ]
    }
    if (defined(config.libace_target)) {
      deps += [ config.libace_target ]
    }

    deps += [ "../utils:napi_utils_static_${platform}" ]

    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
  }
}

foreach(item, ace_platforms) {
  napi_promptaction_static("promptaction_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }
    platform = item.name

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
